// Copyright 2020-2022, Collabora, Ltd.
// SPDX-License-Identifier: BSL-1.0

buildscript {
    ext {
        kotlinVersion = '1.5.31'

        latestAboutLibsRelease = '8.9.4'

        androidxCoreVersion = "1.3.2"
        androidxAnnotationVersion = '1.3.0'
        androidxAppCompatVersion = '1.4.1'
        androidxLifecycleVersion = "2.2.0"
        androidxConstraintLayoutVersion = '2.1.3'
        androidxCardViewVersion = '1.0.0'
        androidxRecyclerViewVersion = '1.2.1'

        hiltVersion = '2.37'

        materialVersion = "1.3.0"

        // This is the version to download if we can't find it locally.
        eigenFetchVersion = '3.4.0'

        // If you update this, must also update .gitlab-ci/config.yml
        buildToolsVersion = "31.0.0"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.mikepenz.aboutlibraries.plugin:aboutlibraries-plugin:${latestAboutLibsRelease}"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hiltVersion"
    }
}
plugins {
    // Android Gradle Plugin
    id 'com.android.application' version '7.0.4' apply false
    id 'com.android.library' version '7.0.4' apply false

    id 'org.jetbrains.kotlin.android' version "$kotlinVersion" apply false

    // Used for getting the eigen dir, explicit python binary, etc. from local.properties
    id 'com.github.b3er.local.properties' version '1.1'

    // For getting git describe data and formatting it how Android wants.
    id "com.gladed.androidgitversion" version "0.4.14"

    // For downloading e.g. the CDDL (for transitive dependencies of hilt)
    id "de.undercouch.download" version "4.1.2"

    // So we don't have to check in generated files: we start with SVGs for vector art.
    id "com.quittle.svg-2-android-vector" version "0.0.6" apply false
}

ext {
    ndk_version = '21.4.7075529'
    // If you update this, must also update .gitlab-ci/config.yml
    sharedTargetSdk = 31
    sharedMinSdk = 26

    // If you are building on Windows, you will need to explicitly set eigenIncludeDir in your
    // local.properties file since the default value provided below only makes sense on *nix
    eigenIncludeDir = project.findProperty('eigenIncludeDir') ?: '/usr/include/eigen3'

    // If you're having trouble with a "can't find python" CMake error, you can specify the path to
    // Python 3 explicitly in local.properties with a property named "pythonBinary"
    pythonBinary = project.findProperty('pythonBinary')
}
